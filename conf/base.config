/*
 * -------------------------------------------------
 * Nextflow config file for Sarek
 * -------------------------------------------------
 * Basic configuration for some process
 * -------------------------------------------------
 */

includeConfig 'genomes.config'
wf_repository = 'maxulysse'

params {
  // set up default params
  containerPath = '' // Path to Singularity images
  docker = false // Don't use Docker to build buildContainers.nf
  download = false // Don't download reference files in buildReferences.nf
  explicitBqsrNeeded = true // Enable recalibration in main.nf
  genome = 'GRCh38' // Default reference genome is GRCh38
  help = false // Don't give help information
  max_cpus = 16 // Base specifications
  max_memory = 128.GB // Base specifications
  max_time = 240.h // Base specifications
  more = false // Don't give version information
  noBAMQC = false // Use BAMQC
  noGVCF = false // HaplotypeCaller will output gVCF as well
  noReports = false // Reports are made by default
  nucleotidesPerSecond = 1000.0 // To estimate interval size by default
  onlyQC = false // All process will be run and not only the QC tools
  outDir = "${PWD}" // Path to output directory
  push = false // Don't push container to DockerHub
  repository = wf_repository // DockerHub containers repository
  singularity = false // Don't use singularity to build buildContainers.nf
  step = 'mapping' // Default step is mapping
  strelkaBP = false // Don't use Manta's candidate indels as input to Strelka
  tag = 'latest' // Default tag is latest, to be overwritten by --tag <version>
  targetBED = false // no targets by default
  test = false // Not testing by default
  verbose = false // Enable for more verbose information
}

process {
  withName:ConcatVCF {
    // For unknown reasons, ConcatVCF sometimes fails with SIGPIPE
    // (exit code 141). Rerunning the process will usually work.
    errorStrategy = {task.exitStatus == 141 ? 'retry' : 'terminate'}
  }
}

process.shell = ['/bin/bash', '-euo', 'pipefail']

report {  // Turning on execution report by default
  enabled = true
  file = "${params.outDir}/Reports/Sarek_report.html"
}

timeline { // Turning on timeline tracking by default
  enabled = true
  file = "${params.outDir}/Reports/Sarek_timeline.html"
}

dag { // Turning on dag by default
  enabled = true
  file = "${params.outDir}/Reports/Sarek_DAG.svg"
}

trace { // Turning on trace tracking by default
  enabled = true
  fields = 'process,task_id,hash,name,attempt,status,exit,realtime,%cpu,vmem,rss,submit,start,complete,duration,realtime,rchar,wchar'
  file = "${params.outDir}/Reports/Sarek_trace.txt"
}

// Function to ensure that resource requirements don't go beyond
// a maximum limit
def check_max(obj, type) {
  if(type == 'memory'){
    try {
      if(obj.compareTo(params.max_memory as nextflow.util.MemoryUnit) == 1)
        return params.max_memory as nextflow.util.MemoryUnit
      else
        return obj
    } catch (all) {
      println "   ### ERROR ###   Max memory '${params.max_memory}' is not valid! Using default value: $obj"
      return obj
    }
  } else if(type == 'time'){
    try {
      if(obj.compareTo(params.max_time as nextflow.util.Duration) == 1)
        return params.max_time as nextflow.util.Duration
      else
        return obj
    } catch (all) {
      println "   ### ERROR ###   Max time '${params.max_time}' is not valid! Using default value: $obj"
      return obj
    }
  } else if(type == 'cpus'){
    try {
      return Math.min( obj, params.max_cpus as int )
    } catch (all) {
      println "   ### ERROR ###   Max cpus '${params.max_cpus}' is not valid! Using default value: $obj"
      return obj
    }
  }
}
